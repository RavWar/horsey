Crafty.audio.add
  bell: ['sounds/bell.mp3', 'sounds/bell.ogg'],
  wheels: ['sounds/wheels.mp3', 'sounds/wheels.ogg'],
  creak: ['sounds/creak.mp3', 'sounds/creak.ogg'],
  hit: ['sounds/hit.mp3', 'sounds/hit.ogg']

Crafty.sprite 210, 270, "<%= asset_path 'main-motion.png' %>",
  PlayerSprite: [0, 0]

Crafty.sprite 520, 300, "<%= asset_path 'drop-motion.png' %>",
  PlayerDropSprite: [0, 0]

Crafty.sprite 140, 50, "<%= asset_path 'stone1.png' %>",
  Stone1Sprite: [0, 0]

Crafty.sprite 118, 50, "<%= asset_path 'stone2.png' %>",
  Stone2Sprite: [0, 0]

Crafty.sprite 114, 52, "<%= asset_path 'stone3.png' %>",
  Stone3Sprite: [0, 0]

Crafty.sprite 100, 50, "<%= asset_path 'stone4.png' %>",
  Stone4Sprite: [0, 0]

Crafty.sprite 194, 120, "<%= asset_path 'object1.png' %>",
  Object1Sprite: [0, 0]

Crafty.sprite 44, 104, "<%= asset_path 'object2.png' %>",
  Object2Sprite: [0, 0]

Crafty.sprite 43, 86, "<%= asset_path 'object3.png' %>",
  Object3Sprite: [0, 0]

Crafty.sprite 41, 120, "<%= asset_path 'object4.png' %>",
  Object4Sprite: [0, 0]

Crafty.sprite 48, 97, "<%= asset_path 'object5.png' %>",
  Object5Sprite: [0, 0]

Crafty.sprite 960, 400, "<%= asset_path 'dirt.png' %>",
  DirtSprite: [0, 0]

Crafty.sprite 65, 95, "<%= asset_path 'bonus-life.png' %>",
  LifeSprite: [0, 0]

Crafty.c 'Grid',
  init: ->
    @requires('2D, DOM').attr
      w: Game.tile.width,
      h: Game.tile.height

  at: (x, y) ->
    if x == undefined && y == undefined
      { x: @x / Game.tile.width, y: @y / (Game.tile.height + 1) }
    else
      @attr
        x: x * Game.tile.width
        y: y * Game.tile.height

Crafty.c 'PlayField',
  init: ->
    @requires('Grid, DirtSprite').attr y: Game.options.header * Game.tile.height
    @bind 'EnterFrame', ->
      if @x <= -Crafty.viewport.x - Crafty.viewport.width
        @x = -Crafty.viewport.x + Crafty.viewport.width

Crafty.c 'Stone',
  init: ->
    @requires('Grid, Solid, Collision')

Crafty.c 'Object',
  init: ->
    @requires('Grid, Solid, Collision')

Crafty.c 'Life',
  init: ->
    @requires('Grid, Solid, Collision, SpriteAnimation, LifeSprite')
    .collision(new Crafty.polygon([0,40], [0,90], [65,90], [65,40]))
    .reel('LifeBounce', 1000, 0, 0, 16).animate('LifeBounce', -1)

Crafty.c 'Scoreboard',
  init: ->
    @score = 0
    @lives = Game.options.lives

  updateScore: (score) ->
    @score = score
    $('#score').text @score

  updateLives: (change) ->
    @lives += change

    if change > 0
      $('#lives').append "<div class='life'>"
    else
      $('.life:last').remove()
      Crafty.audio.play 'hit', 1

Crafty.c 'Player',
  init: (scoreboard) ->
    @requires('Grid, Collision, SpriteAnimation, PlayerSprite')
      .collision(new Crafty.polygon([45,180], [45,280], [200,280], [200,180]))
      .reel('PlayerRunning', 1000, 0, 0, 30)
      .animate('PlayerRunning', -1)
      .attr(x: 100, y: 290, z: 5)
      .onHit('Stone', @stoneHit)
      .onHit('Life', @lifeHit)
      .bindKeyboard()
      .movement()

    setInterval (=>
      Game.speed += 1
    ), 2500

    Crafty.audio.play 'bell', 1

  player: (scoreboard) ->
    @scoreboard = scoreboard

  stoneHit: (object) ->
    # Remove stone collision
    object[0].obj.collision(new Crafty.polygon([]))

    @unbind 'EnterFrame', @movePlayer
    $('body').unbind 'keydown'
    @scoreboard.updateLives -1

    @removeComponent('PlayerSprite').addComponent('PlayerDropSprite')
      .reel('PlayerDropping', 1050, 0, 0, 23).animate('PlayerDropping', -1)

    setTimeout (=>
      return Crafty.stop(true) if @scoreboard.lives <= 0

      x = object[0].obj.x + 50 - @x
      @x += x
      Crafty.viewport.x -= x
      @movement()
      @bindKeyboard()

      @removeComponent('PlayerDropSprite').addComponent('PlayerSprite')
        .reel('PlayerRunning', 1000, 0, 0, 30).animate('PlayerRunning', -1)
    ), 1000

  lifeHit: (object) ->
    @scoreboard.updateLives 1
    object[0].obj.destroy()

  bindKeyboard: ->
    $('body').bind 'keydown', (e) =>
      if e.keyCode == 38 and @y > @calcHeight(Game.options.header) - 220
        @moveLane 'down'
      else if e.keyCode == 40 and @y < @calcHeight(Game.options.lanes) - 240
        @moveLane 'up'
      else if e.keyCode == 80
        Crafty.pause()
    @

  moveLane: (direction) ->
    return if @running

    @running = true
    timesRun = 0

    interval = setInterval (=>
      @y += if direction == 'up' then 4 else -4
      timesRun += 4

      if timesRun >= Game.tile.height
        clearInterval interval
        @running = false
    ), 1

  calcHeight: (lanes) ->
    (lanes + 1) * Game.tile.height

  movement: ->
    @bind 'EnterFrame', @movePlayer

  movePlayer: ->
    @x += Game.speed
    Crafty.viewport.x -= Game.speed
    @scoreboard.updateScore parseInt -Crafty.viewport.x / 200
